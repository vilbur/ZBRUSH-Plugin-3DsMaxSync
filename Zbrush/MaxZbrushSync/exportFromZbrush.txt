
[RoutineDef, subToolVisibilityTest,

	[VarSet, is_visible,	0]
	[VarSet, state, [SubToolGetStatus,id]]
	[VarSet,	find,	[SubToolGetFolderIndex,id]]
	
	[If,(find > -1)
	 ,//it's in a folder
		[VarSet, state_folder,[SubToolGetStatus,find]]//get folder visibility
		
		[If,([Val, state_folder]&0x2 == 0x2)&&([Val, state]&0x1 == 0x1)
		,
			[VarSet, is_visible,1]
		]
	, //else no folder
		[If,([Val, state]&0x1 == 0x1)
		,
			[VarSet,is_visible,1]
		]
	]

// PARAMS
,is_visible, id
]



/** exportFromZbrush
  *
  */
[RoutineDef, exportFromZbrush
,
	[FileNameSetNext, [StrMerge, "C:/Windows/Temp/_ZBRUSH_MAX_SYNC/", $tool_name, "obj"]]

	[IPress,Tool:Export]
	
, // PARAMS
	$tool_name
]

/** exportSubTools
  *
  */
[RoutineDef, exportSubTools
,
	//[Note, "exportSubTools"]
	
	[VarSet, current,[SubToolGetActiveIndex]]

	[Loop, [SubToolGetCount],
	 
		[SubToolSelect, i]
		
		[If, [Var, only_visible] 
		, // THEN
		
			[VarSet, is_visible, 0]
			
			[RoutineCall, subToolVisibilityTest, is_visible, i ]
			
			[If, [Var, is_visible] 
			, // THEN
				[RoutineCall, exportFromZbrush, [IGetTitle, Tool:SubTool:ItemInfo]]
			]
		, // THEN
			[RoutineCall, exportFromZbrush, [IGetTitle, Tool:SubTool:ItemInfo]]
		]

	, i]
		
 	[SubToolSelect, current]
	
, // PARAMS
	only_visible
]

  
/** importToMax
  *
  */
[RoutineDef, importToMax
,
	//[Note, "selectedToMax"]
	[VarSet, import_to_max_ahk, [FileNameResolvePath,  "importToMax.ahk"]]
	[VarSet, err, [FileExecute, "ZFileUtils64.dll", "LaunchAppWithFile",  #import_to_max_ahk]] // execute file
]
//[ IButton, "selectedToMax()" , "", [RoutineCall, selectedToMax] ]


[ IButton, "Selected To Max" , ""
,
	[RoutineCall, exportFromZbrush, [IGetTitle, Tool:SubTool:ItemInfo]]
]


[ IButton, "All To Max" , ""
,
	[RoutineCall, exportSubTools, 0]
]
  
[ IButton, "Visible To Max" , ""
,
	[RoutineCall, exportSubTools, 1]
]
